prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix om:   <http://www.ontology-of-units-of-measure.org/resource/om-2/>
prefix ems:  <https://www.theworldavatar.com/kg/ontoems/>
prefix rt:   <http://environment.data.gov.uk/flood-monitoring/def/core/>

SELECT ?Measurement ?Name ?Unit ?LastUpdate ?MeasurementMethod ?MeasurementPeriod_sec
WHERE {
  # Observations/Measurements: account for different unit relations between MetOffice/AirQuality stations and WaterLevelStations (i.e. EA stations)
  {
    # MetOffice/AirQuality
    [IRI] ems:reports/om:hasValue ?Measurement . 
    ?Measurement ^om:hasValue/rdf:type ?type ; 
                 om:hasUnit/om:symbol ?Unit .                
    BIND (concat(replace(str(?type), str(ems:), ''), ' observation') as ?Name)
  } UNION {
    # EA River Level stations
    [IRI] ems:reports/om:hasValue ?Measurement . 
    ?Measurement rt:parameterName ?Name ;
                 rt:unitName ?Unit .
    # Add additional information (if available)
    OPTIONAL { ?Measurement rt:qualifier ?MeasurementMethod}
    OPTIONAL { ?Measurement rt:period ?MeasurementPeriod_sec}
  }
  # Forecasts: only relevant for MetOffice stations
  UNION {
  [IRI] ems:reports/ems:hasForecastedValue ?Measurement . 
  #?Measurement ^om:hasValue/rdf:type <https://www.theworldavatar.com/kg/ontoems/RelativeHumidity> . 
  ?Measurement ^ems:hasForecastedValue/rdf:type ?type ; 
                ems:createdOn ?LastUpdate ; 
                om:hasUnit/om:symbol ?Unit .            
  BIND (concat(replace(str(?type), str(ems:), ''), ' forecast') as ?Name)    
  }
}